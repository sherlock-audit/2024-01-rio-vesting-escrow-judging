Spare Hotpink Gecko

medium

# Revoking unvested tokens revokes unrealized cliff tokens

## Summary

Cliffs are designed to make a recipient wait X amount of time before they are able to withdraw tokens. This is used to ensure a user  stays committed to a vestment. At any time a manager or owner can revoke unvested tokens via calling VestingEscrow.revokeUnvested(), including before the cliff has expired. Based on how revokeUnvested is designed, tokens not distributed before the cliff length has expired will also be revoked.  This leads to recipients not receiving any tokens that are rightfully theirs.

I'm making several assumptions/rules about this bug:

- Recipients who waited for X amount of time where X < cliffLength deserve at least a pro-rata portion of the cliffed vested tokens.  
- The totalVestedAt() ignores the cliffLength in its formula once the cliffLength is reached. This is important to understand as the totalVestedAt() proportionally returns a value based on how long the recipient waited to receive their rewards. 
- Revoking unvested tokens implies that the recipient deserves some vested rewards regardless if there is a cliff.



## Vulnerability Detail

Let's first look at revokeUnvested():

```solidity
function revokeUnvested() external onlyOwnerOrManager {
    uint256 revokable = locked();
    if (revokable == 0) revert NOTHING_TO_REVOKE();

    disabledAt = uint40(block.timestamp);

    token().safeTransfer(_owner(), revokable);
    emit UnvestedTokensRevoked(msg.sender, revokable);
}
```

The amount that is revoked is based on the return value of locked() which runs `totalLocked() - totalVestedAt()`. This value equals how many unvested (aka locked tokens) are left in the escrow. 

According to the codebase, totalVestedAt will return 0 if this function is called before the cliffLength has been reached. We can see that in the following totalVestedAt(),

```solidity
function _totalVestedAt(uint256 time) internal pure returns (uint256) {
    uint40 _startTime = startTime();
    uint256 _totalLocked = totalLocked();

    // AUDIT: if revokeUnvested() is called before the cliff length ends, 0 will be returned which results in all locked tokens being revoked.
    if (time < _startTime + cliffLength()) {
        return 0;
    }
    return Math.min(_totalLocked * (time - _startTime) / (endTime() - _startTime), _totalLocked);
}
```

Because of this, when revokeUnvested() is called before cliffLength has expired, all tokens will be revoked, which I don't think the protocol intended to do.

### PoC

In the following PoC we simulate what happens when an escrow unvested tokens are revoked, the cliff length has expired, and the user attempts to claim tokens. As you can see the user will receive no tokens, even though they waited beyond the cliff length to claim tokens.

```solidity
// SPDX-License-Identifier: GPL-3.0
pragma solidity 0.8.17;

import {TestUtil} from 'test/lib/TestUtil.sol';
import {IVestingEscrow} from 'src/interfaces/IVestingEscrow.sol';
import {OZVotingAdaptor} from 'src/adaptors/OZVotingAdaptor.sol';
import {IVotes} from '@openzeppelin/contracts/governance/utils/IVotes.sol';

import {ERC20NoReturnToken} from 'test/lib/ERC20NoReturnToken.sol';
import {ERC20Token} from 'test/lib/ERC20Token.sol';
import "forge-std/console2.sol";

contract VestingEscrowTest is TestUtil {
    function setUp() public {
        setUpProtocol(ProtocolConfig({owner: address(1), manager: address(2)}));
        deployVestingEscrow(
            VestingEscrowConfig({
                amount: 1 ether,
                recipient: address(this),
                vestingDuration: 365 days,
                vestingStart: uint40(block.timestamp),
                cliffLength: 90 days,
                isFullyRevokable: true,
                initialDelegateParams: new bytes(0)
            })
        );

         deployVestingEscrow(
            VestingEscrowConfig({
                amount: 1 ether,
                recipient: address(this),
                vestingDuration: 365 days,
                vestingStart: uint40(block.timestamp),
                cliffLength: 90 days,
                isFullyRevokable: true,
                initialDelegateParams: new bytes(0)
            })
        );
    }


    function testCliffUnclaimedBug() public {
        // Step 0: Skip a few days  
        vm.warp(startTime + 3 days);
 
        // Step 1: call revokeUnvested()
        vm.prank(factory.owner());
        deployedVesting.revokeUnvested();
 
        // Step 2: go past cliff length
        vm.warp(startTime + 90 days + 1 days);

        // Step 3: claim
        vm.prank(recipient);
        uint claimed = deployedVesting.claim(recipient, type(uint256).max);

        assertEq(claimed, 0);
    }
}
``` 

## Impact

Recipients will not be able to receive the cliffed "vested" tokens. This is unfair to a recipient who has waited patiently for their tokens to be vested.

## Code Snippet

https://github.com/sherlock-audit/2024-01-rio-vesting-escrow/blob/main/rio-vesting-escrow/src/VestingEscrow.sol?plain=1#L165-L174

https://github.com/sherlock-audit/2024-01-rio-vesting-escrow/blob/main/rio-vesting-escrow/src/VestingEscrow.sol?plain=1#L272-L281

## Tool used

Manual Review

## Recommendation

When revoking unvested tokens, calculate the amount of tokens that should have been vested to the user at the time of revocation and set it aside for the user to claim after the cliff length is reached.  